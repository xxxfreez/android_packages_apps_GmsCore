/*
 * Copyright 2013-2019 microG Project Team
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

configurations {
    withMapboxImplementation
    withVtmImplementation
    withNearbyImplementation
    withoutNearbyImplementation
}

dependencies {
    implementation 'com.squareup.wire:wire-runtime:3.5.0'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'org.conscrypt:conscrypt-android:2.5.1'
    implementation "androidx.core:core-role:1.1.0-alpha01"
    implementation "androidx.core:core-animation:1.0.0-alpha02"
    androidTestImplementation "androidx.core:core-animation-testing:1.0.0-alpha02"
    // TODO: Switch to upstream once raw requests are merged
    // https://github.com/vitalidze/chromecast-java-api-v2/pull/99
    // implementation "su.litvak.chromecast:api-v2:0.11.3"
    implementation "info.armills.chromecast-java-api-v2:api-v2-raw-request:0.10.4-raw-request-1"

    // Specified manually due to
    // https://github.com/vitalidze/chromecast-java-api-v2/issues/91
    api 'org.slf4j:slf4j-api:2.0.0-alpha1'
    api 'uk.uuid.slf4j:slf4j-android:1.7.30-0'

    implementation project(':firebase-dynamic-links-api')
    implementation project(':firebase-auth-core')
    implementation project(':play-services-base-core')
    implementation project(':play-services-base-core-ui')
    implementation project(':play-services-location-core')
    withNearbyImplementation project(':play-services-nearby-core')
    withNearbyImplementation project(':play-services-nearby-core-ui')
    implementation project(':play-services-core-proto')
    implementation project(':play-services-core:microg-ui-tools')
    implementation project(':play-services-api')
    implementation project(':play-services-cast-api')
    implementation project(':play-services-wearable')
    implementation "org.microg:wearable:0.1.1"
    implementation "org.microg.gms:remote-droid-guard:0.1.2"

    withMapboxImplementation project(':play-services-maps-core-mapbox')
    withVtmImplementation project(':play-services-maps-core-vtm')

    // AndroidX UI
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.appcompat:appcompat:1.3.0-alpha02"
    implementation "androidx.mediarouter:mediarouter:1.2.0"
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.1'

    implementation 'androidx.lifecycle:lifecycle-service:2.3.0-beta01'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.20-RC'
}

android {
    compileSdkVersion androidCompileSdk()
    buildToolsVersion "$androidBuildVersionTools"

    defaultConfig {
        versionName version
        versionCode appVersionCode

        minSdkVersion androidMinSdk
        targetSdkVersion androidTargetSdk

        multiDexEnabled true
        multiDexKeepProguard file('multidex-keep.pro')

        ndk {
            abiFilters  "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
        withNearby {
            java.srcDirs += 'src/withNearby/kotlin'
        }
        withoutNearby {
            java.srcDirs += 'src/withoutNearby/kotlin'
        }
    }

    lintOptions {
        disable 'MissingTranslation', 'InvalidPackage', 'BatteryLife', 'ImpliedQuantity', 'MissingQuantity', 'InvalidWakeLockTag'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'maps', 'nearby'
    productFlavors {
        withMapbox {
            dimension 'maps'
        }
        withVtm {
            dimension 'maps'
            versionNameSuffix '-vtm'
        }
        withNearby {
            dimension 'nearby'
        }
        withoutNearby {
            dimension 'nearby'
            versionNameSuffix '-noen'
        }
    }

    compileOptions {
        dexOptions{
            javaMaxHeapSize "8g"
        }
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
    }
}

if (file('user.gradle').exists()) {
    apply from: 'user.gradle'
}
